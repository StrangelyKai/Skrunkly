//Separate from the menus because you could put other interactions here, like poking or booping or punching or asking about various hotspots
OnMouseDoubleClick
{
	if reference5 == 0 //reference5 is the mouse button being clicked, in this case a left click
	{
		//reference3 is which character is being clicked
		//If you want to add double click interactions, reference4 will be the name of the hitbox the mouse is over
		if reference3 == 0
		{
			if reference4 == "leftear"
			{
				OnPreferenceMenu
			}
			elseif reference4 == "rightear"
			{
				OnTalkRateMenu
			}
			// elseif reference4 == "rightfoot"
			// {
				// OnStatsPage
			// }
			elseif reference4 == "righthand"
			{
				OnHoldHands
			}
			elseif reference4 == "lefthand"
			{
				OnHoldHands
			}
			elseif reference4 == "nose"
			{
				OnBoop
			}
			else
			{
				OnAiTalk
			}
		}
		else
		{
			
		}
	}
}

OnNeedlePoke
{
	if reference0 == 0 //Sakura
	{
		needlepokes++
		case reference1
		{
			when "Eggdle"
			{
				"eggy"
			    "fried egg for dinner tonight"
			}
			when "Hands Included"
			{
				"pat the skrunkly"
			}
			when "Safety Needle"
			{
				"safety ouch"
			}
			when "Tiny Pokey Pixels"
			{
				"small ouch"
				"so smol, yet so ow"
			}
			when "Wobbly Mouse Art"
			{
				"wobble wobble wobble wobble"
			}
			when "Birthday Needle"
			{
				"nom"
				"yummy"
				"thank you for this meal"
			}
			when "Colored Pencils"
			{
				"neatly coloured"
				"coloured ouch"
			}
			when "Foil"
			{
				"my plans have been foiled"
			}
			when "Syringe"
			{
				"Please don't take my blood."
			}
			others
			{
				"sharp"
		        "pointy"
		        "ouchy"
			}
		}
	}
}

//Referenced from Needle, with help from Zi
OnMouseGesture
{
	if accessmode == 0
	{
		if !ISVAR("gesture"); gesture = IARRAY
		if reference5 == "end"
		{
			foreach gesture; _g
			{
				if _g == "circle.cw"
				{
					trgesture = "up"
					OnTalkRateGesture
				}
				elseif _g == "circle.ccw"
				{
					trgesture = "down"
					OnTalkRateGesture
				}
			}
		
			_gesture = TOSTR(gesture)
			gesture = IARRAY
		
			if _gesture == "right,left,right,left" || _gesture == "left,right,left,right"
			{
				aitalkinterval = 0
				--
				OnTalkRateCheck
				--
				"Talkrate: %(ltalkrate)"
			}
			elseif _gesture == "left_down,right,left_down"
			{
				//"success"
			}
		}
		else; gesture ,= reference5
	}
}

OnTalkRateGesture
{
	OnTalkRateCheck
	
	if trgesture == "up"
	{
		if aitalkinterval == 0; aitalkinterval = 30
		elseif aitalkinterval == 30; aitalkinterval = 60
		elseif aitalkinterval == 60; aitalkinterval = 120
		elseif aitalkinterval == 120; aitalkinterval = 180
		elseif aitalkinterval == 180; aitalkinterval = 240
		elseif aitalkinterval == 240; aitalkinterval = 300
		elseif aitalkinterval == 300; aitalkinterval = 0
		--
		OnTalkRateCheck
		--
		"Talkrate: %(ltalkrate)"
	}
	elseif trgesture == "down"
	{
		if aitalkinterval == 0; aitalkinterval = 300
		elseif aitalkinterval == 30; aitalkinterval = 0
		elseif aitalkinterval == 60; aitalkinterval = 30
		elseif aitalkinterval == 120; aitalkinterval = 60
		elseif aitalkinterval == 180; aitalkinterval = 120
		elseif aitalkinterval == 240; aitalkinterval = 180
		elseif aitalkinterval == 300; aitalkinterval = 240
		--
		OnTalkRateCheck
		--
		"Talkrate: %(ltalkrate)"
	}
}

OnTalkRateCheck
{
	ltalkrate = "Off"
	if aitalkinterval == 30; ltalkrate = "30 seconds"
	elseif aitalkinterval == 60; ltalkrate = "1 minute"
	elseif aitalkinterval == 120; ltalkrate = "2 minutes"
	elseif aitalkinterval == 180; ltalkrate = "3 minutes"
	elseif aitalkinterval == 240; ltalkrate = "4 minutes"
	elseif aitalkinterval == 300; ltalkrate = "5 minutes"
}

OnMouseWheel
{
	OnMouseMove
}

OnMouseMove
{
	if reference4 != ""
	{
		stroke++
		
		if stroke % 40 == 0
		{
			if reference3 == 0; OnMouseMove.Sakura
		}
	}
}

OnMouseMove.Sakura
{
	if reference4 == "head"
	{
		OnPatPat
	}
}

OnPatPat : nonoverlap
{
	"%(b_fluff)"
	patpatv++
	--
	"Petting??"
	"pat pat pat"
	"pet pet pet"
	"you pat?"
	"fluff fluff"
	"floof floof"
	"puff puff"
	"%(kaomoji('happy'))"
	"%(kaomoji('cat'))"
	"%(kaomoji('bunny'))"
}

OnHoldHands : nonoverlap
{
	holdhandv++
	--
	"holdin hands"
	"i didnt wash my hands"
	"my hands are shaking"
	"%(kaomoji('happy'))"
	"%(kaomoji('cat'))"
	"%(kaomoji('bunny'))"
}

OnBoop : nonoverlap
{
	boopv++
	--
	"boop"
	"bloop"
	"doop"
	"%(kaomoji('happy'))"
	"%(kaomoji('cat'))"
	"%(kaomoji('bunny'))"
}

OnMouseLeaveAll
{
	stroke = 0
}
